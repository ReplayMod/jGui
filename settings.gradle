rootProject.name = 'jGui'

def f = file('versions/core/mcVersion')
def coreMcVersion = f.exists() ? f.readLines().first() as int : 11302

def versions = []
if (gradle.gradleVersion.startsWith('2.')) {
    if (coreMcVersion <= 10800) { // allow 1.8 only to run The Preprocessor
        versions += ['core']
    }
    versions += [
            '1.8', // allow 1.8 only to run The Preprocessor
            '1.7.10',
    ]
} else {
    if (coreMcVersion > 10710) {
        versions += ['core']
    }
    versions += [
            '1.8',
            '1.8.9',
            '1.9.4',
            '1.12',
            '1.13.2',
            '1.14.1',
    ]
}
versions.each { version ->
    include ":$version"
    project(":$version").with {
        projectDir = file("versions/$version")
        buildFileName = '../common.gradle'
    }
}

