buildscript {
    def mcVersion
    if (project.name != 'core') {
        def (major, minor, patch) = project.name.tokenize('.')
        mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
    } else {
        def f = file('mcVersion')
        mcVersion = f.exists() ? f.readLines().first() as int : 11200
    }
    project.ext.mcVersion = mcVersion

    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:' + [
                11200: '2.3-SNAPSHOT',
                10904: '2.2-SNAPSHOT',
                10809: '2.1-SNAPSHOT',
                10800: '2.0-SNAPSHOT',
                10710: '1.2-SNAPSHOT',
        ][mcVersion]
    }
}

if (mcVersion >= 10800) {
    apply plugin: 'net.minecraftforge.gradle.forge'
} else {
    apply plugin: 'forge'
}

group 'de.johni0702.minecraft'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.projectlombok:lombok:1.16.6'
}

minecraft {
    version = [
            11200: '1.12.2-14.23.0.2486',
            10904: '1.9.4-12.17.0.1976',
            10809: '1.8.9-11.15.1.1722',
            10800: '1.8-11.14.4.1563',
            10710: '1.7.10-10.13.4.1558-1.7.10',
    ][mcVersion]
    mappings = [
            11200: 'snapshot_20170615',
            10904: 'snapshot_20160518',
            10809: 'stable_22',
            10800: 'snapshot_nodoc_20141130',
            10710: 'stable_12',
    ][mcVersion]
}

if (project.name != 'core') {
    apply from: '../preprocessor.gradle'

    def preprocessedSrc = 'build/preprocessed/src'
    def preprocessedRes = 'build/preprocessed/res'
    def originalSrc = '../../src/main/java'
    def originalRes = '../../src/main/resources'
    def vars = [MC: mcVersion as int]

    sourceSets {
        main.java.srcDir preprocessedSrc
        main.resources.srcDir preprocessedRes
    }

    task preprocessJava {
        inputs.dir(originalSrc)
        outputs.dir(preprocessedSrc)
        doLast {
            project.convertTree(vars, originalSrc, preprocessedSrc)
        }
    }

    task preprocessResources {
        inputs.dir(originalRes)
        outputs.dir(preprocessedRes)
        doLast {
            project.convertTree(vars, originalRes, preprocessedRes)
        }
    }

    compileJava.dependsOn preprocessJava
    processResources.dependsOn preprocessResources
} else {
    sourceSets {
        main.java.srcDirs = ['../../src/main/java']
        main.resources.srcDirs = ['../../src/main/resources']
    }
}

