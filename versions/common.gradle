buildscript {
    def mcVersion
    def (major, minor, patch) = project.name.tokenize('-')[0].tokenize('.')
    mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
    def fabric = mcVersion >= 11400 && !project.name.endsWith("-forge")
    project.ext.mcVersion = mcVersion
    project.ext.fabric = fabric

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven {
            name = "fabric"
            url = "https://maven.fabricmc.net/"
        }
        if (!fabric) {
            maven {
                name = "forge"
                url = "https://files.minecraftforge.net/maven"
            }
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
        classpath('ch.raffael.gradlePlugins.preshadow:gradle-preshadow-plugin:1.0'){
            exclude group: 'com.github.jengelman.gradle.plugins', module: 'shadow'
        }
        if (fabric) {
            classpath 'fabric-loom:fabric-loom.gradle.plugin:0.2.2-SNAPSHOT'
        } else if (mcVersion >= 11400) {
            classpath('net.minecraftforge.gradle:ForgeGradle:3.+'){
                exclude group: 'trove', module: 'trove' // preprocessor/idea requires more recent one
            }
        } else if (mcVersion >= 10800) {
            classpath('com.github.ReplayMod:ForgeGradle:' + (
                    mcVersion >= 11200 ? 'b64de9c' : // FG 2.3
                    mcVersion >= 10904 ? '5bb7a53' : // FG 2.2
                                         'fc1eabc'   // FG 2.1
            ) + ':all')
        } else {
            classpath 'com.github.ReplayMod:ForgeGradle:e93f77b4' // FG 1.2
        }
    }
}

def FG3 = !fabric && mcVersion >= 11400
def FABRIC = fabric

if (FABRIC) {
    apply plugin: 'fabric-loom'
} else if (FG3) {
    apply plugin: 'net.minecraftforge.gradle'
} else if (mcVersion >= 10800) {
    apply plugin: 'net.minecraftforge.gradle.forge'
} else {
    apply plugin: 'forge'
}
apply plugin: 'ch.raffael.preshadow'
apply plugin: 'com.replaymod.preprocess'

preprocess {
    vars.put("MC", project.mcVersion)
    vars.put("FABRIC", project.fabric ? 1 : 0)
}

group 'de.johni0702.minecraft.jgui'
version 'git'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (mcVersion >= 11400) {
    sourceSets {
        api
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "fabric"
        url = "https://maven.fabricmc.net/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.16.6'
    compile 'org.projectlombok:lombok:1.16.6'

    preshadow('org.lwjgl.lwjgl:lwjgl_util:2.9.3') {
        transitive = false // only want col, dim, point, vec, mat, quat, etc.
    }
}

preshadowJar {
    relocate 'org.lwjgl.util', 'de.johni0702.minecraft.gui.utils.lwjgl'
}

if (FABRIC) {
    minecraft {
        refmapName = 'mixins.jgui.refmap.json'
    }
} else {
    minecraft {
        if (!FG3) {
            version = [
                    11200: '1.12.2-14.23.0.2486',
                    10904: '1.9.4-12.17.0.1976',
                    10809: '1.8.9-11.15.1.1722',
                    10800: '1.8-11.14.4.1563',
                    10710: '1.7.10-10.13.4.1558-1.7.10',
            ][mcVersion]
        }
        mappings = [
                11404: "snapshot_20190719-1.14.3",
                11200: 'snapshot_20170615',
                10904: 'snapshot_20160518',
                10809: 'stable_22',
                10800: 'snapshot_nodoc_20141130',
                10710: 'stable_12',
        ][mcVersion]
    }
}

dependencies {
    if (FABRIC) {
        minecraft 'com.mojang:minecraft:' + [
                11404: '1.14.4',
                11502: '1.15.2',
        ][mcVersion]
        mappings 'net.fabricmc:yarn:' + [
                11404: '1.14.4+build.16',
                11502: '1.15.2+build.14',
        ][mcVersion]
        modCompile 'net.fabricmc:fabric-loader:' + [
                11404: '0.4.8+build.155',
                11502: '0.7.8+build.189',
        ][mcVersion]
        modCompile 'net.fabricmc.fabric-api:fabric-api:' + [
                11404: '0.3.0+build.200',
                11502: '0.5.1+build.294-1.15',
        ][mcVersion]
    }
    if (FG3) {
        minecraft 'net.minecraftforge:forge:' + [
                11404: '1.14.4-28.1.113',
        ][mcVersion]
    }
}
