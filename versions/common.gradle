buildscript {
    def mcVersion
    if (project.name != 'core') {
        def (major, minor, patch) = project.name.tokenize('.')
        mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
    } else {
        def f = file('mcVersion')
        mcVersion = f.exists() ? f.readLines().first() as int : 11302
    }
    project.ext.mcVersion = mcVersion

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        if (mcVersion >= 11400) {
            maven {
                name = "fabric"
                url = "https://maven.fabricmc.net/"
            }
        } else {
            maven {
                name = "forge"
                url = "https://files.minecraftforge.net/maven"
            }
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        if (!gradle.gradleVersion.startsWith('3.')) {
            classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2' // for Gradle 4
        } else {
            classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4' // for Gradle 3
        }
        classpath('ch.raffael.gradlePlugins.preshadow:gradle-preshadow-plugin:1.0'){
            exclude group: 'com.github.jengelman.gradle.plugins', module: 'shadow'
        }
        if (mcVersion >= 11400) {
            classpath 'fabric-loom:fabric-loom.gradle.plugin:0.2.2-SNAPSHOT'
        } else {
            classpath 'net.minecraftforge.gradle:ForgeGradle:' + [
                    11302: '3.+',
                    11200: '2.3-SNAPSHOT',
                    10904: '2.2-SNAPSHOT',
                    10809: '2.1-SNAPSHOT',
                    10800: '2.0-SNAPSHOT',
                    10710: '1.2-SNAPSHOT',
            ][mcVersion]
        }
        classpath 'com.github.replaymod:preprocessor:ba743e3'
    }
}

def FG3 = mcVersion >= 11300 && mcVersion < 11400
def FABRIC = mcVersion >= 11400

if (FABRIC) {
    apply plugin: 'fabric-loom'
} else if (FG3) {
    apply plugin: 'net.minecraftforge.gradle'
} else if (mcVersion >= 10800) {
    apply plugin: 'net.minecraftforge.gradle.forge'
} else {
    apply plugin: 'forge'
}
apply plugin: 'ch.raffael.preshadow'
apply plugin: 'com.replaymod.preprocess'

group 'de.johni0702.minecraft'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "fabric"
        url = "https://maven.fabricmc.net/"
    }
}

dependencies {
    if (!gradle.gradleVersion.startsWith('3.')) {
        annotationProcessor 'org.projectlombok:lombok:1.16.6'
    }
    compile 'org.projectlombok:lombok:1.16.6'

    preshadow('org.lwjgl.lwjgl:lwjgl_util:2.9.3') {
        transitive = false // only want col, dim, point, vec, mat, quat, etc.
    }
}

preshadowJar {
    relocate 'org.lwjgl.util', 'de.johni0702.minecraft.gui.utils.lwjgl'
}

if (FABRIC) {
    minecraft {
    }
} else {
    minecraft {
        if (!FG3) {
            version = [
                    11200: '1.12.2-14.23.0.2486',
                    10904: '1.9.4-12.17.0.1976',
                    10809: '1.8.9-11.15.1.1722',
                    10800: '1.8-11.14.4.1563',
                    10710: '1.7.10-10.13.4.1558-1.7.10',
            ][mcVersion]
        }
        mappings = [
                11302: 'snapshot_20180921-1.13',
                11200: 'snapshot_20170615',
                10904: 'snapshot_20160518',
                10809: 'stable_22',
                10800: 'snapshot_nodoc_20141130',
                10710: 'stable_12',
        ][mcVersion]
    }
}

dependencies {
    if (FABRIC) {
        minecraft 'com.mojang:minecraft:' + [
                11400: '1.14'
        ][mcVersion]
        mappings 'net.fabricmc:yarn:' + [
                11400: '1.14+build.1'
        ][mcVersion]
        modCompile 'net.fabricmc:fabric-loader:' + [
                11400: '0.4.2+build.132'
        ][mcVersion]
        modCompile 'net.fabricmc:fabric:' + [
                11400: '0.2.7+build.127'
        ][mcVersion]
    } else {
        // For Event, EventFactory, etc.
        // Not strictly required for jGui itself but we want to expose our utility types (e.g. EventRegistrations)
        // to consuming mods which may use them on all versions (e.g. ReplayMod uses it for all its internal events).
        compile('net.fabricmc.fabric-api:fabric-lib:0.1.0') {
            transitive = false
        }
    }
    if (FG3) {
        minecraft 'net.minecraftforge:forge:' + [
                11302: '1.13.2-25.0.63',
        ][mcVersion]
    }
}
